import LeanCourse.Common
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv
import Mathlib.Analysis.Calculus.Deriv.Prod
import Mathlib.Analysis.Calculus.Deriv.Pow
import Mathlib.Analysis.SpecialFunctions.Integrals
import Mathlib.Analysis.Convolution
import Mathlib.Data.Real.Irrational
import Mathlib.MeasureTheory.Function.Jacobian
open BigOperators Function Set Real Topology Filter
open MeasureTheory Interval Convolution ENNReal
noncomputable section





/-
# Practical remarks
* Assignment 9 is due 10.12. Upload it to eCampus.
-/

/- # Last Week -/

/-
Last time we discussed topology:
* filters are generalized sets, and can capture notions
  like "very large numbers" (`atTop`) or
  "points close to `x`" (`ùìù x`).
* `‚àÄ·∂† x in F, P x` states that `x` holds eventually in `F`.
* `TopologicalSpace X` states that `X` is a topological space.
* `MetricSpace X` states that `X` is a metric space.
-/

/-
Today: calculus: differentiation and integration.
-/




/- # Differential Calculus -/

/- We write `deriv` to compute the derivative of a function.
`simp` can compute the derivatives of standard functions. -/

example (x : ‚Ñù) : deriv Real.sin x = Real.cos x := by simp

example (x : ‚ÑÇ) :
    deriv (fun y ‚Ü¶ Complex.sin (y + 3)) x = Complex.cos (x + 3) := by simp

/- Not every function has a derivative.
As usual, in mathlib we just define the derivative
of a non-differentiable function to be `0`. -/

variable (f : ‚Ñù ‚Üí ‚Ñù) (x : ‚Ñù) in
#check (deriv_zero_of_not_differentiableAt :
  ¬¨ DifferentiableAt ‚Ñù f x ‚Üí deriv f x = 0)

/- So proving that `deriv f x = y` doesn't
necessarily mean that `f` is differentiable.
Often it is nicer to use the predicate `HasDerivAt f y x`,
which states that `f` is differentiable and `f'(x) = y`. -/

example (x : ‚Ñù) : HasDerivAt Real.sin (Real.cos x) x :=
  hasDerivAt_sin x


/- We can also specify that a function has a derivative
without specifying its derivative. -/

example (x : ‚Ñù) : DifferentiableAt ‚Ñù sin x :=
  (hasDerivAt_sin x).differentiableAt


/- Mathlib contains lemmas stating that common operations satisfy
`HasDerivAt` and `DifferentiableAt` and to compute `deriv`. -/

#check HasDerivAt.add
#check deriv_add
#check DifferentiableAt.add


example (x : ‚Ñù) :
    HasDerivAt (fun x ‚Ü¶ Real.cos x + Real.sin x)
    (Real.cos x - Real.sin x) x := by {
  sorry
  }


/- There are various variations of derivatives/being differentiable -/

/- A function is differentiable everywhere. -/
#check Differentiable

/- A function is differentiable on a subset. -/
#check DifferentiableOn

/- A function is differentiable at a point, considered only within the subset -/
#check DifferentiableWithinAt

/- We can also consider the derivative only within a subset. -/
#check HasDerivWithinAt
#check derivWithin




/-
Lean has the following names for intervals
("c" = closed, "o" = open, "i" = infinity)
Icc a b = [a, b]
Ico a b = [a, b)
Ioc a b = (a, b]
Ioo a b = (a, b)
Ici a   = [a, ‚àû)
Ioi a   = (a, ‚àû)
Iic b   = (-‚àû, b]
Iio b   = (-‚àû, b)

The intermediate value theorem states that if `f` is continuous and
`f a ‚â§ y ‚â§ f b`, then there is an `x ‚àà [a, b]` with `f(x) = y`.
-/

example {f : ‚Ñù ‚Üí ‚Ñù} {a b y : ‚Ñù} (hab : a ‚â§ b)
    (hf : ContinuousOn f (Icc a b)) :
    Icc (f a) (f b) ‚äÜ f '' Icc a b :=
  intermediate_value_Icc hab hf


/-
The mean value theorem states that if `f` is continuus on `[a, b]`
and differentiable on `(a, b)` then there is a `c ‚àà (a, b)` where `f'(c)` is the
average slope of `f` on `[a, b]`
-/
example (f : ‚Ñù ‚Üí ‚Ñù) {a b : ‚Ñù} (hab : a < b)
    (hf : ContinuousOn f (Icc a b))
    (hf' : DifferentiableOn ‚Ñù f (Ioo a b)) :
    ‚àÉ c ‚àà Ioo a b, deriv f c = (f b - f a) / (b - a) :=
  exists_deriv_eq_slope f hab hf hf'


/- Rolle's theorem is the special case where `f a = f b`.
Why is there no differentiability requirement on `f` here? -/
example {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù} (hab : a < b)
    (hfc : ContinuousOn f (Icc a b)) (hfI : f a = f b) :
    ‚àÉ c ‚àà Ioo a b, deriv f c = 0 :=
  exists_deriv_eq_zero hab hfc hfI



/- We can more generally talk about the derivative of functions between normed spaces.

A *normed group* is an abelian group with a norm satisfying the following rules.
-/

section NormedGroup

variable {E : Type*} [NormedAddCommGroup E]

#check (fun x ‚Ü¶ ‚Äñx‚Äñ : E ‚Üí ‚Ñù)

example (x : E) : 0 ‚â§ ‚Äñx‚Äñ :=
  norm_nonneg x

example {x : E} : ‚Äñx‚Äñ = 0 ‚Üî x = 0 :=
  norm_eq_zero

example (x y : E) : ‚Äñx + y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ :=
  norm_add_le x y

/- This turns `E` into a metric space. -/
example (x y : E) : dist x y = ‚Äñx - y‚Äñ :=
  dist_eq_norm x y

/- A *normed space* is a normed group that is a vector space
satisfying the following condition. -/

variable [NormedSpace ‚Ñù E]

example (a : ‚Ñù) (x : E) : ‚Äña ‚Ä¢ x‚Äñ = |a| * ‚Äñx‚Äñ :=
  norm_smul a x


/- A complete normed space is known as a *Banach space*.
Every finite-dimensional vector space is complete. -/

example [FiniteDimensional ‚Ñù E] : CompleteSpace E := by infer_instance

/- In the above examples, we could also replace `‚Ñù` by `‚ÑÇ`
or another *normed field*. -/

end NormedGroup



/- We can also take the derivative of functions that take values in a
normed vector space. -/

example (x : ‚Ñù) : deriv (fun x ‚Ü¶ ((Real.cos x) ^ 2, (Real.sin x) ^ 2)) x =
    (- 2 * Real.cos x * Real.sin x, 2 * Real.sin x * Real.cos x) := by {
  sorry
  }



/- If the domain is a normed space we can define the
total derivative, which will be a continuous linear map. -/

/- Morphisms between normed spaces are continuous linear maps `E ‚ÜíL[ùïú] F`. -/
section NormedSpace

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]


example : E ‚ÜíL[ùïú] E := ContinuousLinearMap.id ùïú E

example (f : E ‚ÜíL[ùïú] F) : E ‚Üí F := f

example (f : E ‚ÜíL[ùïú] F) : Continuous f := f.cont

example (f : E ‚ÜíL[ùïú] F) : E ‚Üí‚Çó[ùïú] F := f

/- Continuous linear maps have an operator norm. -/

example (f : E ‚ÜíL[ùïú] F) (x : E) : ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ * ‚Äñx‚Äñ :=
  f.le_opNorm x

example (f : E ‚ÜíL[ùïú] F) {M : ‚Ñù} (hMp : 0 ‚â§ M)
    (hM : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ M * ‚Äñx‚Äñ) : ‚Äñf‚Äñ ‚â§ M :=
  f.opNorm_le_bound hMp hM


/- We define the *Fr√©chet derivative* of any function between normed spaces. -/

example (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x‚ÇÄ : E) :
    HasFDerivAt f f' x‚ÇÄ ‚Üî
    Tendsto (fun x ‚Ü¶ ‚Äñf x - f x‚ÇÄ - f' (x - x‚ÇÄ)‚Äñ / ‚Äñx - x‚ÇÄ‚Äñ) (ùìù x‚ÇÄ) (ùìù 0) := by
  simp_rw [div_eq_inv_mul, hasFDerivAt_iff_tendsto]

example (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x‚ÇÄ : E) (hff' : HasFDerivAt f f' x‚ÇÄ) :
    fderiv ùïú f x‚ÇÄ = f' :=
  hff'.fderiv

/- We can take the directional derivative or partial derivative
by applying the Fr√©chet derivative to an argument -/
example (x y : ‚Ñù) :
    let f := fun ((x,y) : ‚Ñù √ó ‚Ñù) ‚Ü¶ x^2 + x * y
    fderiv ‚Ñù f (x, y) (1, 0) = 2 * x + y := by
  sorry -- exercise


/- We write `ContDiff ùïú n f` to say that `f` is `C^n`,
i.e. it is `n`-times continuously differentiable.
Here `n` lives in `‚Ñï‚àû`,
which is `‚Ñï` with an extra top element `‚ä§` added ("‚àû").
`fun_prop` can prove that simple functions are
differentiable, C^n, measurable, ...
(though it is not super reliable) -/
variable {f g : E ‚Üí F} {n : ‚Ñï‚àû} {r : ùïú}
example (hf : ContDiff ùïú n f) (hg : ContDiff ùïú n g) :
    ContDiff ùïú n (fun x ‚Ü¶ (f x, r ‚Ä¢ f x + g x)) := by
  fun_prop

example : ContDiff ùïú 0 f ‚Üî Continuous f := contDiff_zero

example {n : ‚Ñï} : ContDiff ùïú (n+1) f ‚Üî
    Differentiable ùïú f ‚àß ContDiff ùïú n (fderiv ùïú f) :=
  contDiff_succ_iff_fderiv

example : ContDiff ùïú ‚ä§ f ‚Üî ‚àÄ n : ‚Ñï, ContDiff ùïú n f :=
  contDiff_top

end NormedSpace





/-! # Integration -/

example (a b : ‚Ñù) : ‚à´ x in a..b, x = (b ^ 2 - a ^ 2) / 2 :=
  integral_id

example (f : ‚Ñù ‚Üí ‚Ñù) : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶
  ‚à´ t in (0)..x, f t

example (a b : ‚Ñù) : ‚à´ x in a..b, exp x = exp b - exp a :=
  integral_exp

/- the notation `[[a, b]]` (in namespace `Interval`) means
`uIcc a b`, i.e. the interval from `min a b` to `max a b` -/
example {a b : ‚Ñù} (h : (0 : ‚Ñù) ‚àâ [[a, b]]) :
    ‚à´ x in a..b, 1 / x = log (b / a) :=
  integral_one_div h

example (a b : ‚Ñù) :
    ‚à´ x in a..b, exp (x + 3) = exp (b + 3) - exp (a + 3) := by
  simp

/- We have the fundamental theorem of calculus in Lean. -/

/- FTC-1 -/
example (f : ‚Ñù ‚Üí ‚Ñù) (hf : Continuous f) (a b : ‚Ñù) :
    deriv (fun u ‚Ü¶ ‚à´ x : ‚Ñù in a..u, f x) b = f b :=
  (hf.integral_hasStrictDerivAt a b).hasDerivAt.deriv

/- FTC-2 -/
example {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù} {f' : ‚Ñù ‚Üí ‚Ñù}
    (h : ‚àÄ x ‚àà [[a, b]], HasDerivAt f (f' x) x)
    (h' : IntervalIntegrable f' volume a b) :
    ‚à´ y in a..b, f' y = f b - f a :=
  intervalIntegral.integral_eq_sub_of_hasDerivAt h h'

/- We can use this to compute integrals
if we know the antiderivative. -/

example (a b : ‚Ñù) : ‚à´ x in a..b, exp (x + 3) =
    exp (b + 3) - exp (a + 3) := by {
  sorry
  }




/- The measure of a set lives in the
extended non-negative reals `[0, ‚àû]`. -/
#check ‚Ñù‚â•0‚àû
example : ‚Ñù‚â•0‚àû = WithTop {x : ‚Ñù // 0 ‚â§ x} := rfl
example : (‚àû + 5) = ‚àû := by simp
example : (‚àû * 0) = 0 := by simp


/- More generally, Mathlib develops Lebesgue integration,
which requires measure theory.

The basic notion is that of a "œÉ-algebra".
A œÉ-algebra is called `MeasurableSpace` in Lean.
It consists of a collection of sets, called the *measurable* sets.
The empty set is measurable,
and the measurable sets are closed under
complementation and countable unions. -/

variable {X : Type*} [MeasurableSpace X]

example : MeasurableSet (‚àÖ : Set X) :=
  MeasurableSet.empty

example {s : Set X} (hs : MeasurableSet s) : MeasurableSet (s·∂ú) :=
  hs.compl

example {f : ‚Ñï ‚Üí Set X} (h : ‚àÄ b, MeasurableSet (f b)) : MeasurableSet (‚ãÉ b, f b) :=
  MeasurableSet.iUnion h



/-
We now can define measures.

On paper, a measure on a set equipped with a œÉ-algebra
is a function from the measurable sets to `[0, ‚àû]`
that is additive on countable disjoint unions.

In Mathlib, we denote `[0, ‚àû]` by `ENNReal`.
We extend the measure to any set `s`
as the infimum of measures of measurable sets containing `s`.
Of course, many lemmas still require measurability assumptions,
but some can be proven without measurability.
-/

variable {Œº : Measure X}

example : Œº ‚àÖ = 0 :=
  measure_empty

example {s : ‚Ñï ‚Üí Set X} (hmeas : ‚àÄ i, MeasurableSet (s i))
    (hdis : Pairwise (Disjoint on s)) :
    Œº (‚ãÉ i, s i) = ‚àë' i, Œº (s i) :=
  Œº.m_iUnion hmeas hdis

example (s : Set X) : Œº s = ‚®Ö (t ‚äá s) (h : MeasurableSet t), Œº t :=
  measure_eq_iInf s

example (s : ‚Ñï ‚Üí Set X) : Œº (‚ãÉ i, s i) ‚â§ ‚àë' i, Œº (s i) :=
  measure_iUnion_le s

/- We say that a property `P` holds **almost everywhere**
if the set of elements where it doesn't hold has measure 0. -/
example {P : X ‚Üí Prop} :
    (‚àÄ·∂† x in ae Œº, P x) ‚Üî Œº {x | ¬¨ P x} = 0 := by
  rfl

/- This also has the specific notation `‚àÄ·µê (x : X) ‚àÇŒº, P x`. -/
variable (P : X ‚Üí Prop) in
#check ‚àÄ·∂† x in ae Œº, P x

/- Various spaces have a canonical measure associated to them,
called `volume`. -/
example : MeasureSpace ‚Ñù := by infer_instance
#check (volume : Measure ‚Ñù)
#check (volume : Measure (Fin 3 ‚Üí ‚Ñù))

/- The function `ENNReal.toReal` sends `‚àû` to `0`. -/
example (a b : ‚Ñù) (h : a ‚â§ b) :
    (volume (Icc a b)).toReal = b - a := by
  simp [h]

/- The collection of measurable sets on `‚Ñù`
is the smallest œÉ-algebra containing the open sets.
These are the *Borel-measurable* sets. -/
example : BorelSpace ‚Ñù := by infer_instance


/- The *Lebesgue-measurable* sets are the sets
that are Borel measurable up to a null set. -/
#check NullMeasurableSet
example {s : Set ‚Ñù} (hs : volume s = 0) : NullMeasurableSet s := by
  exact?

/- The collection of measurable sets on `‚Ñù`
is the smallest œÉ-algebra containing the open sets.

Remark: `rw` will not rewrite inside a binder
(like `fun x`, `‚àÉ x`, `‚à´ x` or `‚àÄ·∂† x`).
Use `simp_rw`, `simp only` or `unfold` instead. -/
example : ‚àÄ·µê x : ‚Ñù, Irrational x := by {
  sorry
  }


/- A map is (Borel)-measurable if preimages of measurable sets
under that map are measurable. -/
#print Measurable
#check Continuous.measurable

/- A map `f` into a normed group is integrable when it is measurable and the map
`x ‚Ü¶ ‚Äñf x‚Äñ` has a finite integral. -/
#check Integrable

example : ¬¨ Integrable (fun x ‚Ü¶ 1 : ‚Ñù ‚Üí ‚Ñù) := by {
  sorry
  }





/- We can take the integrals for functions intro a Banach space.
This version of the integral is called the *Bochner integral*.
The integral is denoted `‚à´ a, f x ‚àÇŒº` -/
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [CompleteSpace E] {f : X ‚Üí E}

example {f g : X ‚Üí E} (hf : Integrable f Œº) (hg : Integrable g Œº) :
    ‚à´ x, f x + g x ‚àÇŒº = ‚à´ x, f x ‚àÇŒº + ‚à´ x, g x ‚àÇŒº :=
  integral_add hf hg


/-
* We can write `‚à´ x in s, f x ‚àÇŒº` for the integral restricted to a set.
* In the following example, we compute the integral of a constant map
-/
example {s : Set X} (c : E) :
    ‚à´ x in s, c ‚àÇŒº = (Œº s).toReal ‚Ä¢ c :=
  setIntegral_const c

/-
* We can abbreviate `‚à´ x, f x ‚àÇvolume` to `‚à´ x, f x`
* We write `‚à´ x in a..b, f x ‚àÇŒº` for the integral
  on an interval (whose sign is reversed if `b < a`)
-/
example {f : ‚Ñù ‚Üí E} {a b c : ‚Ñù} : ‚à´ x in a..b, c ‚Ä¢ f x = c ‚Ä¢ ‚à´ x in a..b, f x :=
  intervalIntegral.integral_smul c f

example {f : ‚Ñù ‚Üí E} {a b : ‚Ñù} : ‚à´ x in b..a, f x = - ‚à´ x in a..b, f x :=
  intervalIntegral.integral_symm a b


/- Here is a version of the dominated convergence theorem. -/
example {F : ‚Ñï ‚Üí X ‚Üí E} {f : X ‚Üí E} (bound : X ‚Üí ‚Ñù)
    (hmeas : ‚àÄ n, AEStronglyMeasurable (F n) Œº)
    (hint : Integrable bound Œº) (hbound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚ÄñF n x‚Äñ ‚â§ bound x)
    (hlim : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n : ‚Ñï ‚Ü¶ F n x) atTop (ùìù (f x))) :
    Tendsto (fun n ‚Ü¶ ‚à´ x, F n x ‚àÇŒº) atTop (ùìù (‚à´ x, f x ‚àÇŒº)) :=
  tendsto_integral_of_dominated_convergence bound hmeas hint hbound hlim


/- Here is the statement of Fubini's theorem. -/
variable {X Y : Type*} [MeasurableSpace X] {Œº : Measure X} [SigmaFinite Œº]
    [MeasurableSpace Y] {ŒΩ : Measure Y} [SigmaFinite ŒΩ] in
example (f : X √ó Y ‚Üí E) (hf : Integrable f (Œº.prod ŒΩ)) :
    ‚à´ z, f z ‚àÇ Œº.prod ŒΩ = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº :=
  integral_prod f hf


/- There are various versions of the change of variables theorem. -/
example {s : Set ‚Ñù} {f f' : ‚Ñù ‚Üí ‚Ñù}
    (hs : MeasurableSet s)
    (hf' : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x)
    (hf : InjOn f s) (g : ‚Ñù ‚Üí E) : ‚à´ x in f '' s, g x = ‚à´ x in s, |f' x| ‚Ä¢ g (f x) :=
  integral_image_eq_integral_abs_deriv_smul hs hf' hf g
