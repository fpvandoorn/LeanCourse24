import LeanCourse.Common
import LeanCourse.DiffGeom
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.VectorBundle.Hom
import Mathlib.Geometry.Manifold.VectorBundle.Pullback
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv

open Set ENat Manifold Metric Module Bundle Function
noncomputable section





/-
# Practical remarks
* Assignment 10 does not require you to hand-in anything.
Work on your project instead.
-/

/- ## Last Week -/

/-
Last time we discussed differential and integral calculus:
* `DifferentiableAt`, `HasDerivAt` and `deriv` are used
  to talk about derivatives of "single-variable" functions.
* `HasFDerivAt` and `fderiv` are used to talk about the
  FrÃ©chet derivative (total derivative) of a function
  whose domain is a normed space.
* We use `âˆ« x in a..b, f x`, `âˆ« x in A, f x` or `âˆ« x, f x`
  for integration. You can add `âˆ‚Î¼` at the end to specify that
  it is integration w.r.t. the measure `Î¼`.
* `MeasurableSpace` associates a standard Ïƒ-algebra to a type.
* Mathlib has standard theorems:
  - intermediate value theorem
  - mean value theorem
  - dominated converge theorem
  - fundamental theorem of calculus
  - change of variable theorem
  - etc.
-/



/- # Today: Differential Geometry -/


/- Ingredient needed for smooth manifolds (glatte Mannigfaltigkeit):
A *partial homeomorphism* from `X â†’ Y` is a homeomorphism
from an open subset `s âŠ† X` to an open subset `t âŠ† Y`. -/

-- An equivalence between a subset of the domain and a subset of the codomain
#check PartialEquiv
-- A homeomorphism between open subsets of the domain and codomain
#check PartialHomeomorph


/- A topological space is locally euclidean if you have a
partial homeomorphism to `â„â¿` around each point in `X`. -/
#check ChartedSpace


/- A smooth manifold is `X` an charted space structure
such that for any two charts the coordinate change function
between the charts is smooth on their common domain. -/
variable {n : â„•}
  {M : Type*} [TopologicalSpace M] [ChartedSpace (Fin n â†’ â„) M]
  {e e' : PartialHomeomorph M (Fin n â†’ â„)}

/- We want to require the following condition for smooth manifolds. -/
#check ContDiffOn â„ âŠ¤ (e.symm â‰«â‚• e') (e.symm â‰«â‚• e').source

/- This is captured by the predicate `HasGroupoid`. -/
#check HasGroupoid

/- We can also equip a manifold with another groupoid structure,
to define the class of `C^k` manifolds or analytic manifolds,
or other classes of manifolds. -/
#check StructureGroupoid



/- Here `contDiffGroupoid` specifies the groupoid structure
on partial homeomorphisms stating that coordinate change functions
must be smooth -/
#check contDiffGroupoid



/- `e.symm â‰«â‚• e' : â„â¿ â†’ â„â¿` is the coordinate change function from `e` to `e'`. -/
example [SmoothManifoldWithCorners ğ“˜(â„, Fin n â†’ â„) M]
    {e e' : PartialHomeomorph M (Fin n â†’ â„)}
    (he : e âˆˆ atlas (Fin n â†’ â„) M) (he' : e' âˆˆ atlas (Fin n â†’ â„) M) :
    ContDiffOn â„ âŠ¤ (e.symm â‰«â‚• e') (e.symm â‰«â‚• e').source := by
  have := (contDiffGroupoid âŠ¤ ğ“˜(â„, Fin n â†’ â„)).compatible he he'
  simpa [contDiffPregroupoid] using this.1



/- ## General manifolds -/

/- The general definition of manifolds in mathlib is
more general than this example:
* It can be over any normed field, such as `â„`, `â„‚` or the p-adic numbers;
* It can have infinite dimension;
* It can have boundaries or corners. -/



/- To cover manifolds with boundaries and corners,
every manifold `M` is modelled by a *model space* `H`.
There is a map `I : H â†’ E` where `E` is a normed space over a field `ğ•œ`.

Example: `E = â„â¿`, `H` is a half-space, and `I : H â†’ E` is the inclusion.
This map `I` is called a *model with corners*.
`ğ“¡ n` is notation for the identity map `â„â¿ â†’ â„â¿`.
`ğ“¡âˆ‚ n` is the inclusion from the half-space into `â„â¿` -/
#check ModelWithCorners
#check ğ“¡ n
#check ğ“¡âˆ‚ 3

#check SmoothManifoldWithCorners

section examples

section unitInterval
open unitInterval

#check I -- I is notation for the interval [0, 1]

/- The interval [0, 1] is modelled by two charts with model space [0, âˆ),
so it is a topological manifold -/
#synth ChartedSpace (EuclideanHalfSpace 1) I

/- To state that it is a smooth manifold, we have to say
that all coordinate changes live in the groupoid of smooth maps -/
#synth HasGroupoid I (contDiffGroupoid âˆ (ğ“¡âˆ‚ 1))

/- This is the same as saying that `I` forms a smooth manifold. -/
#synth SmoothManifoldWithCorners (ğ“¡âˆ‚ 1) I

/- Atlases are not maximal in general, but we can use `maximalAtlas`
to consider a maximal atlas. -/
#check (contDiffGroupoid âˆ (ğ“¡âˆ‚ 1)).maximalAtlas I

end unitInterval



/- The sphere in a finite-dimensional inner product space is a smooth manifold -/

variable (n : â„•) (E : Type*) [NormedAddCommGroup E]
  [InnerProductSpace â„ E] [Fact (finrank â„ E = n + 1)]

#synth SmoothManifoldWithCorners (ğ“¡ n) (sphere (0 : E) 1)

/- The map ğ•Šâ¿ â†ª â„â¿âºÂ¹ is smooth -/
example : Smooth (ğ“¡ n) ğ“˜(â„, E) ((Â·) : sphere (0 : E) 1 â†’ E) := by
  exact?

/- The circle is a Lie group -/
example : LieGroup (ğ“¡ 1) Circle := by
  infer_instance



/- Declaring a general smooth manifold is a little verbose. -/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]

variable
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [SmoothManifoldWithCorners I M]
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [TopologicalSpace G] (J : ModelWithCorners ğ•œ F G)
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [SmoothManifoldWithCorners J N]



/- ## Tangent space & tangent bundle -/

/- A differentiable map between manifolds induces a map on tangent spaces. -/

example (f : M â†’ N) (hf : MDifferentiable I J f) (x : M) :
    TangentSpace I x â†’L[ğ•œ] TangentSpace J (f x) :=
  mfderiv I J f x

example {f g : M â†’ M} (x : M)
    (hg : MDifferentiableAt I I g (f x)) (hf : MDifferentiableAt I I f x) :
    mfderiv I I (g âˆ˜ f) x = (mfderiv I I g (f x)).comp (mfderiv I I f x) :=
  mfderiv_comp x hg hf



/- It also induces a map on the tangent bundle. -/

example (f : M â†’ N) (hf : MDifferentiable I J f) :
    TangentBundle I M â†’ TangentBundle J N :=
  tangentMap I J f

example (f : M â†’ N) (hf : ContMDiff I J âŠ¤ f) :
    ContMDiff I.tangent J.tangent âŠ¤ (tangentMap I J f) :=
  hf.contMDiff_tangentMap le_rfl


example [AddGroup N] [LieAddGroup J N] {f g : M â†’ N} {n : â„•âˆ}
    (hf : ContMDiff I J n f) (hg : ContMDiff I J n g) :
    ContMDiff I J n (f + g) :=
  hf.add hg





/- ## Smooth vector bundles -/

/- Given a continuous surjection `Ï€ : Z â†’ M`.
A trivialization of `Ï€` at `x : M` with fiber `F`
is a neighborhood `U` of `x` and a homeomorphism
`Ï• : Ï€ â»Â¹' U â†’ U Ã— F` that commutes with projections. -/
#check Trivialization

/- Fiber bundles have trivializations around each point in the base manifold -/
#check FiberBundle

/- In vector bundles the trivializations induce linear maps on the fibers.
Interestingly, for infinite-dimensional manifolds
you need an additional continuity condition. -/
#check VectorBundle

/- In smooth vector bundles the trivializations are smooth. -/
#check SmoothVectorBundle







/- Let `Eâ‚` and `Eâ‚‚` be smooth vector bundles over `M`. -/

variable
  (Fâ‚ : Type*) [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  (Eâ‚ : M â†’ Type*) [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)]
  [âˆ€ x : M, TopologicalSpace (Eâ‚ x)] [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)]
  [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [SmoothVectorBundle Fâ‚ Eâ‚ I]
variable
  (Fâ‚‚ : Type*) [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  (Eâ‚‚ : M â†’ Type*) [âˆ€ x, AddCommGroup (Eâ‚‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚‚ x)]
  [âˆ€ x : M, TopologicalSpace (Eâ‚‚ x)] [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  [SmoothVectorBundle Fâ‚‚ Eâ‚‚ I]


/- The product of two bundles is a smooth vector bundle. -/

#synth SmoothVectorBundle (Fâ‚ Ã— Fâ‚‚) (Eâ‚ Ã—áµ‡ Eâ‚‚) I

/- We can take construct the bundle of continuous linear maps between bundles. -/

variable [âˆ€ x, TopologicalAddGroup (Eâ‚ x)] [âˆ€ x, TopologicalAddGroup (Eâ‚‚ x)]
  [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚‚ x)]

#synth SmoothVectorBundle (Fâ‚ â†’L[ğ•œ] Fâ‚‚)
    (Bundle.ContinuousLinearMap (.id ğ•œ) Eâ‚ Eâ‚‚) I

/- We can pull back vector bundles. -/

variable (f : C^âˆâŸ®J, N; I, MâŸ¯)

#synth SmoothVectorBundle Fâ‚ ((f : N â†’ M) *áµ– Eâ‚) J






/- Patrick Massot, Oliver Nash and I have formalized
a result in differential geometry called *Gromov's h-principle*.

In particular, this allows you to abstractly define an eversion of a sphere. -/

def Immersion (f : M â†’ N) : Prop := âˆ€ m, Injective (mfderiv I J f m)

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E]
  [Fact (finrank â„ E = 3)]

local notation "â„Â³" => E
local notation "ğ•ŠÂ²" => sphere (0 : â„Â³) 1

theorem sphere_eversion : âˆƒ f : â„ â†’ ğ•ŠÂ² â†’ â„Â³,
    (ContMDiff (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, â„Â³) âˆ (uncurry f)) âˆ§
    (f 0 = fun x : ğ•ŠÂ² â†¦ (x : â„Â³)) âˆ§
    (f 1 = fun x : ğ•ŠÂ² â†¦ -(x : â„Â³)) âˆ§
    âˆ€ t, Immersion (ğ“¡ 2) ğ“˜(â„, â„Â³) (f t) :=
  sorry -- not yet in mathlib

end examples
